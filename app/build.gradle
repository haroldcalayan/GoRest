plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

apply from: "config.gradle"
apply from: "dependencies.gradle"

def secretKeyPropertiesFile = file("secretkey.properties")
def secretKeyProperties = new Properties()
if (secretKeyPropertiesFile.exists()) {
    secretKeyProperties.load(new FileInputStream(secretKeyPropertiesFile))
}

android {
    compileSdkVersion configs.compileSdkVersion
    buildToolsVersion configs.buildToolsVersion

    defaultConfig {
        applicationId configs.applicationId
        minSdkVersion configs.minSdkVersion
        targetSdkVersion configs.targetSdkVersion
        versionCode configs.versionCode
        versionName configs.versionName
        buildConfigField "String", "DATABASE_NAME", DATABASE_NAME

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file('keystore/release.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            zipAlignEnabled false
            manifestPlaceholders = [enableCrashReporting: "false"]
            buildConfigField "String", "BASE_GOREST_API_URL", DEVELOPMENT_BASE_GOREST_API_URL

            if (secretKeyPropertiesFile.exists()) {
                buildConfigField("String", "GOREST_API_TOKEN", secretKeyProperties['DEVELOPMENT_GOREST_API_TOKEN'])
            } else {
                buildConfigField("String", "GOREST_API_TOKEN", "\"${System.env.DEVELOPMENT_GOREST_API_TOKEN}\"")
            }
        }
        staging {
            debuggable true
            minifyEnabled false
            zipAlignEnabled false
            manifestPlaceholders = [enableCrashReporting: "true"]
            buildConfigField "String", "BASE_GOREST_API_URL", STAGING_BASE_GOREST_API_URL

            if (secretKeyPropertiesFile.exists()) {
                buildConfigField("String", "GOREST_API_TOKEN", secretKeyProperties['STAGING_GOREST_API_TOKEN'])
            } else {
                buildConfigField("String", "GOREST_API_TOKEN", "\"${System.env.STAGING_GOREST_API_TOKEN}\"")
            }
        }
        release {
            debuggable false
            minifyEnabled true
            zipAlignEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [enableCrashReporting: "true"]
            buildConfigField "String", "BASE_GOREST_API_URL", PRODUCTION_BASE_GOREST_API_URL

            if (secretKeyPropertiesFile.exists()) {
                buildConfigField("String", "GOREST_API_TOKEN", secretKeyProperties['PRODUCTION_GOREST_API_TOKEN'])
            } else {
                buildConfigField("String", "GOREST_API_TOKEN", "\"${System.env.PRODUCTION_GOREST_API_TOKEN}\"")
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        dataBinding = true
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = outputFileName.replace("app", "GoRest-v" + defaultConfig.versionName)
        }
    }
}

dependencies {

    // Kotlin
    implementation kotlinStdLib
    implementation kotlinXCoroutine

    // AndroidX
    implementation androidXDependencies.activityKTX
    implementation androidXDependencies.annotation
    implementation androidXDependencies.appCompat
    implementation androidXDependencies.constraintLayout
    implementation androidXDependencies.coreKTX
    implementation androidXDependencies.lifecycleExtension
    implementation androidXDependencies.lifecycleLiveDataKTX
    implementation androidXDependencies.lifecycleRuntimeKTX
    implementation androidXDependencies.lifecycleViewModelKTX
    implementation androidXDependencies.navigationFragment
    implementation androidXDependencies.navigationFragmentKTX
    implementation androidXDependencies.navigationUI
    implementation androidXDependencies.navigationUIKTX
    implementation androidXDependencies.recyclerView
    implementation androidXDependencies.roomKTX
    implementation androidXDependencies.roomRuntime
    implementation androidXDependencies.roomRxJava2
    implementation androidXDependencies.sqlite
    kapt androidXDependencies.roomCompiler

    // Google
    implementation googleDependencies.material

    // Retrofit
    implementation retrofitDependencies.adapterRxJava2
    implementation retrofitDependencies.retrofit
    implementation retrofitDependencies.converterGson
    implementation retrofitDependencies.converterMoshi

    // OkHttp
    implementation okHttpLoggingInterceptorDependency

    // OkHttp Profiler
    implementation okHttpProfilerDependency

    // Moshi
    implementation moshiDependencies.moshiKotlin
    kapt moshiDependencies.moshiKotlinCodeGen

    // Hilt
    implementation hiltDependencies.hiltAndroid
    kapt hiltDependencies.hiltAndroidCompiler

    // Timber
    implementation timberDependency

    // Glide
    implementation glideDependencies.glide
    kapt glideDependencies.glideCompiler

    // Firebase
    implementation platform(firebaseDependencies.bom)
    implementation firebaseDependencies.analyticsKTX
    implementation firebaseDependencies.crashlyticsKTX
    implementation firebaseDependencies.config

    // Unit Test
    testImplementation jUnitDependency
    androidTestImplementation androidXTestDependencies.espresso
    androidTestImplementation androidXTestDependencies.jUnitExt
}